plugins {
  id 'java-library'
  // To create a shadow/fat jar, including dependencies
  id 'com.github.johnrengelman.shadow' version '7.0.0'
  // To manage included native libraries
  id 'org.bytedeco.gradle-javacpp-platform' version '1.5.5'
}

repositories {
  mavenCentral()
  
  maven {
    url "https://maven.scijava.org/content/repositories/releases"
  }
  
  maven {
    url "https://maven.scijava.org/content/repositories/snapshots"
  }
  
}

ext.moduleName = 'qupath.extension.tensorflow'

description = 'QuPath extension to use TensorFlow'

version = "0.3.0-SNAPSHOT"

dependencies {
    def tensorflowVersion = "0.3.2"
    def platform = project.findProperty("platform")
    
    if (platform) {
        implementation "org.tensorflow:tensorflow-core-platform-${platform}:${tensorflowVersion}"
    } else {
        implementation "org.tensorflow:tensorflow-core-platform:${tensorflowVersion}"
    }
    shadow "io.github.qupath:qupath-gui-fx:0.3.0-SNAPSHOT"
    shadow "org.slf4j:slf4j-api:1.7.30"
}

processResources {
  from ("${projectDir}/LICENSE.txt") {
    into 'licenses/'
  }
}

tasks.register("copyDependencies", Copy) {
    description "Copy dependencies into the build directory for use elsewhere"
    group "QuPath"
    
    from configurations.default
    into 'build/libs'
}

/*
 * Ensure Java 11 compatibility
 */
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    if (project.properties['sources'])
	    withSourcesJar()
    if (project.properties['javadocs'])
	    withJavadocJar()
}

/*
 * Create javadocs for all modules/packages in one place.
 * Use -PstrictJavadoc=true to fail on error with doclint (which is rather strict).
 */
def strictJavadoc = findProperty('strictJavadoc')
if (!strictJavadoc) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}
    
/*
 * Avoid 'Entry .gitkeep is a duplicate but no duplicate handling strategy has been set.'
 * when using withSourcesJar()
 */
tasks.withType(org.gradle.jvm.tasks.Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}